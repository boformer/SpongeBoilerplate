// Import plugins
buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
	}
}

// Extended project information. Edit these values!
ext.projectName = 'Notifier'
ext.packaging = 'jar'
ext.author = 'SPBP'
ext.authorUrl = 'https://github.com/spbp'
ext.inceptionYear = '2015'

// Set subprojects' versions. Edit these values! 
// Don't forget to edit the project names.
// You can set different version numbers for your API and implementation.
project(':notifier-api').version = '0.1.0'
project(':notifier-plugin').version = '0.2.1'

// Everything in the subprojects closure is automatically applied to all subprojects.
subprojects {
	// Apply plugins
	apply plugin: 'java' // Support for Java
	apply plugin: 'maven' // Maven integration
	apply plugin: 'license' // Automatic license headers
	apply plugin: 'eclipse' // Helper plugin for IDE
	apply plugin: 'idea' // Helper plugin for IDE
	
	// Set compatibility
	sourceCompatibility = 1.7
	targetCompatibility = 1.7
	
	// Maven group
	group = 'com.github.spbp.notifier'
	
	// Configure license headers
	license {
		ext.name = rootProject.projectName
		ext.author = rootProject.author
		ext.url = rootProject.authorUrl
		ext.year = rootProject.inceptionYear
		exclude "**/*.info"
		exclude "**/*.html"
		exclude "assets/**"
		header new File(rootProject.getProjectDir(), "HEADER.txt")
		sourceSets = project.sourceSets
		ignoreFailures false
		strictCheck true
		mapping {
			java = 'SLASHSTAR_STYLE'
		}
	}
	
	// Task to generate source jar
		task sourcesJar(type: Jar, dependsOn: classes) {
		classifier = "sources"
		from sourceSets.main.allSource
	}
	
	// Task to generate javadoc jar
	task javadocJar(type: Jar, dependsOn: javadoc) {
		classifier = "javadoc"
		from javadoc.destinationDir
	}
	
	// Include LICENSE.txt in all jars
	processResources {
		from new File(rootProject.getProjectDir(), "LICENSE.txt")
	}
	
	// Define compiled jar, sources jar and javadoc jar as Maven artifacts
	artifacts {
		archives jar
		archives sourcesJar
		archives javadocJar
	}
	
	// Add custom jar tasks to build task
	build.dependsOn(sourcesJar)
	build.dependsOn(javadocJar)
	
	// Equivalent to mvn install for your latest artifacts
	uploadArchives {
		repositories {
			mavenDeployer {
				// mavenUrl has to be set in your {userfolder}/.gradle/gradle.properties file
				// Example file content: 
				// mavenUrl=file:///C://mvn-repo/
				if (project.hasProperty("mavenUrl")) {
					repository(url: mavenUrl)
				}
				pom {
					groupId = project.group
					artifactId = project.name
					version = project.version
				}
			}
		}
	}
}
